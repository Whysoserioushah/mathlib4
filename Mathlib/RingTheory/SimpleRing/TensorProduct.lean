/-
Copyright (c) 2025 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang, Yunzhou Xie
-/
import Mathlib
import Mathlib.RingTheory.TwoSidedIdeal.SpanAsSum

/-!
# Tensor product of simple algebras

# Tensor product of simple algebras over a field

In this file, we show that the tensor product of a simple algebra and a central simple algebra is
simple, which in particular implies that the tensor product of two central simple algebras is
another central simple algebra. This is a prerequisite for defining the group law of the Brauer
group.

## Main Results

* `TensorProduct.nontrivial`: The tensor product of two non-trivial algebras is non-trivial.
* `TensorProduct.simple`: The tensor product of a simple algebra and a central simple algebra
  is simple.

## References

* [StackProject 074B](https://stacks.math.columbia.edu/tag/074B)

## Tags
Noncommutative algebra, tensor product, simple algebra, central simple algebra

-/

universe u v v‚ÇÅ v‚ÇÇ v‚ÇÉ

variable (K : Type u) [Field K]
  {A : Type v‚ÇÅ} {B : Type v‚ÇÇ} {C : Type v‚ÇÉ} [Ring A] [Ring B] [Ring C]
  [Algebra K A] [Algebra K B] [Algebra K C]

open scoped TensorProduct

open Module
variable {K} in
/--
a non-zero element in an ideal that can be represented as a sum of tensor products of `n`-terms.
-/
structure is_obtainable_by_sum_tmul
    {ŒπA A B : Type*} [Ring A] [Algebra K A] [Ring B] [Algebra K B]
    (x : A ‚äó[K] B) (ùíú : Basis ŒπA K A) (I : TwoSidedIdeal (A ‚äó[K] B)) (n : ‚Ñï) : Prop where
  mem : x ‚àà I
  ne_zero : x ‚â† 0
  rep : ‚àÉ (s : Finset ŒπA) (_ : s.card = n) (f : ŒπA ‚Üí B),
    x = ‚àë i ‚àà s, ùíú i ‚äó‚Çú[K] f i

variable {K} in
lemma is_obtainable_by_sum_tmul.exists_minimal_element
    {A B : Type*} [Ring A] [Algebra K A] [Ring B] [Algebra K B]
    (ŒπA : Type*) (ùíú : Basis ŒπA K A)
    (I : TwoSidedIdeal (A ‚äó[K] B)) (hI : I ‚â† ‚ä•) :
    ‚àÉ (n : ‚Ñï) (x : A ‚äó[K] B), is_obtainable_by_sum_tmul x ùíú I n ‚àß
      ‚àÄ (m : ‚Ñï) (y : A ‚äó[K] B) , is_obtainable_by_sum_tmul y ùíú I m ‚Üí n ‚â§ m := by
  classical
  have := SetLike.ext_iff.not.mp hI
  push_neg at this
  obtain ‚ü®x, ‚ü®hx0, hx1‚ü©|‚ü®hx0, hx1‚ü©‚ü© := this
  ¬∑ obtain ‚ü®s, rfl‚ü© := TensorProduct.eq_repr_basis_left ùíú x
    let n := @Nat.find (fun n => ‚àÉ x : A ‚äó[K] B, is_obtainable_by_sum_tmul x ùíú I n) _
      ‚ü®s.support.card, ‚àë i ‚àà s.support, ùíú i ‚äó‚Çú[K] s i, ‚ü®hx0, hx1, s.support, rfl, s, rfl‚ü©‚ü©
    obtain ‚ü®x, hx‚ü© : ‚àÉ x, is_obtainable_by_sum_tmul x ùíú I n :=
      @Nat.find_spec (fun n => ‚àÉ x : A ‚äó[K] B, is_obtainable_by_sum_tmul x ùíú I n) _
      ‚ü®s.support.card, ‚àë i ‚àà s.support, ùíú i ‚äó‚Çú[K] s i, ‚ü®hx0, hx1, s.support, rfl, s, rfl‚ü©‚ü©
    refine ‚ü®n, x, hx, fun m y hy => ?_‚ü©
    by_contra r
    simp only [not_le] at r
    have := @Nat.find_min (fun n => ‚àÉ x : A ‚äó[K] B, is_obtainable_by_sum_tmul x ùíú I n) _
      ‚ü®s.support.card, ‚àë i ‚àà s.support, ùíú i ‚äó‚Çú[K] s i, ‚ü®hx0, hx1, s.support, rfl, s, rfl‚ü©‚ü© m r
    simp only [not_exists] at this
    exact this y hy
  ¬∑ change x = 0 at hx1
    subst hx1
    exact hx0 I.zero_mem |>.elim

-- lemma TensorProduct.sum_tmul_basis_right_eq_zero'
--     (B : Type*) [Ring B] [Algebra K B]
--     (C : Type*) [Ring C] [Algebra K C]
--     {ŒπC : Type*} (ùíû : Basis ŒπC K C)
--     (s : Finset ŒπC) (b : ŒπC ‚Üí B)
--     (h : ‚àë i ‚àà s, b i ‚äó‚Çú[K] ùíû i = 0) :
--     ‚àÄ i ‚àà s, b i = 0 := by
--   classical
--   intro i
--   have := TensorProduct.sum_tmul_basis_right_eq_zero (Œ∫ := ŒπC) ùíû (M := B)
--     { support := s.filter fun i ‚Ü¶ b i ‚â† 0
--       toFun := fun x => if x ‚àà s then b x else 0
--       mem_support_toFun := by simp }
--     (by
--       simp only [Finsupp.sum, ne_eq, Finsupp.coe_mk, Finset.sum_filter, ite_not]
--       rw [‚Üê h]
--       congr!
--       aesop)
--   simpa using Finsupp.ext_iff.mp this i

-- lemma TensorProduct.sum_tmul_basis_left_eq_zero'
--     (B : Type*) [Ring B] [Algebra K B]
--     (C : Type*) [Ring C] [Algebra K C]
--     {ŒπB : Type*} (‚Ñ¨ : Basis ŒπB K B)
--     (s : Finset ŒπB) (c : ŒπB ‚Üí C)
--     (h : ‚àë i ‚àà s, ‚Ñ¨ i ‚äó‚Çú[K] c i = 0) :
--     ‚àÄ i ‚àà s, c i = 0 := by
--   classical
--   have := TensorProduct.sum_tmul_basis_left_eq_zero (Œπ := ŒπB) ‚Ñ¨ (N := C)
--     { support := s.filter fun i ‚Ü¶ c i ‚â† 0
--       toFun := fun x => if x ‚àà s then c x else 0
--       mem_support_toFun := by simp }
--     (by
--       simp only [Finsupp.sum, ne_eq, Finsupp.coe_mk, Finset.sum_filter, ite_not]
--       rw [‚Üê h]
--       congr!
--       aesop)
--   simpa? using Finsupp.ext_iff.mp this
  -- apply TensorProduct.sum_tmul_basis_right_eq_zero' K C B ‚Ñ¨ s c
  -- apply_fun TensorProduct.comm K B C at h
  -- simpa using h

-- instance TensorProduct.nontrivial
--     (A B : Type v) [Ring A] [Algebra K A] [Ring B] [Algebra K B]
--     [Nontrivial A] [Nontrivial B] :
--     Nontrivial (A ‚äó[K] B) :=
--   nontrivial_of_linearMap_injective_of_flat_right K A B (Algebra.linearMap _ _)
--     (FaithfulSMul.algebraMap_injective _ _)

theorem _root_.TwoSidedIdeal.mem_map_of_mem {R S : Type*}
    [NonUnitalNonAssocRing R] [NonUnitalNonAssocRing S]
    {F : Type*} [FunLike F R S] {f : F} {I : TwoSidedIdeal R}
    {x : R} (hx : x ‚àà I) : f x ‚àà I.map f :=
  TwoSidedIdeal.subset_span ‚ü®x, hx, rfl‚ü©

theorem _root_.Submodule.mem_span_range_iff_exists' {Œ± M R : Type*}
    [Semiring R] [AddCommMonoid M] [Module R M] {v : Œ± ‚Üí M} {x : M} :
    x ‚àà Submodule.span R (Set.range v) ‚Üî ‚àÉ (s : Finset Œ±) (c : Œ± ‚Üí R), ‚àë i ‚àà s, c i ‚Ä¢ v i = x := by
  classical
  rw [‚Üê Set.image_univ, Submodule.mem_span_image_iff_exists_fun]
  simp only [Set.subset_univ, Finset.univ_eq_attach, true_and, ‚Üê Finset.sum_attach (Œπ := Œ±)]
  refine ‚ü®fun ‚ü®s, c, hsc‚ü© ‚Ü¶ ‚ü®s, fun x ‚Ü¶ if h : x ‚àà s then c ‚ü®x, h‚ü© else 0, ?_‚ü©,
    fun ‚ü®s, c, hsc‚ü© ‚Ü¶ ‚ü®s, c ‚àò Subtype.val, by simpa‚ü©‚ü©
  convert hsc
  grind

-- attribute [local instance] Algebra.TensorProduct.rightAlgebra in
lemma TensorProduct.map_comap_eq_zero_if_zero
    {A B : Type v} [DivisionRing A] [Algebra K A] [Ring B] [Algebra K B]
    [isCentral_A : Algebra.IsCentral K A]
    [isSimple_B : IsSimpleRing B]
    (I : TwoSidedIdeal (A ‚äó[K] B))
    (hAB : letI f : B ‚Üí‚Çê[K] A ‚äó[K] B := Algebra.TensorProduct.includeRight
      (I.comap f).map f = ‚ä•) : I = ‚ä• := by
  set f : B ‚Üí‚Çê[K] A ‚äó[K] B := Algebra.TensorProduct.includeRight
  obtain ‚ü®Œπ, ùìë‚ü© := Module.Free.exists_basis K B
  have main (s : Finset Œπ) (a : Œπ ‚Üí A) (h : ‚àë i ‚àà s, a i ‚äó‚Çú[K] ùìë i ‚àà I) :
      ‚àÄ i ‚àà s, a i = 0 := by
    classical
    induction s using Finset.induction_on generalizing a with
    | empty => simp
    | insert j s hjs ih =>
    rcases eq_or_ne (a j) 0 with hj | hj
    ¬∑ aesop
    ¬∑ replace h := I.mul_mem_left ((a j)‚Åª¬π ‚äó‚Çú 1) _ h
      simp_rw [Finset.mul_sum, Algebra.TensorProduct.tmul_mul_tmul,
        one_mul, Finset.sum_insert hjs, inv_mul_cancel‚ÇÄ hj] at h
      have key : ‚àÄ i : s, ‚àÉ k, (a j)‚Åª¬π * a i = algebraMap K A k := by
        have (c : A) := I.sub_mem (I.mul_mem_left (c ‚äó‚Çú 1) _ h) (I.mul_mem_right _ (c ‚äó‚Çú 1) h)
        simp_rw [mul_add, add_mul, add_sub_add_comm, Algebra.TensorProduct.tmul_mul_tmul,
          mul_one, one_mul, sub_self, zero_add, Finset.mul_sum, Finset.sum_mul,
          ‚Üê Finset.sum_sub_distrib, Algebra.TensorProduct.tmul_mul_tmul, mul_one, one_mul,
          ‚Üê sub_tmul] at this
        exact fun i ‚Ü¶ (Algebra.IsCentral.mem_center_iff K).mp <| Subalgebra.mem_center_iff.mpr
          fun c ‚Ü¶ sub_eq_zero.mp <| ih _ (this c) i i.2
      choose k hk using key
      rw [‚Üê Finset.sum_attach] at h
      simp_rw [hk] at h
      set key : B := ùìë j + ‚àë i ‚àà s.attach, k i ‚Ä¢ ùìë i
      have hkey : f key ‚àà I := by
        convert h using 1
        simp [f, key, tmul_add, tmul_sum, -tmul_smul, ‚Üê smul_tmul, ‚Üê Algebra.algebraMap_eq_smul_one]
      replace hkey : f key = 0 :=
        eq_bot_iff.mp hAB <| TwoSidedIdeal.mem_map_of_mem <| (TwoSidedIdeal.mem_comap _).mpr hkey
      replace hkey := (map_eq_zero_iff _ f.toRingHom.injective).mp hkey
      set g : Œπ ‚Üí K := fun i ‚Ü¶ if h : i ‚àà s then k ‚ü®i, h‚ü© else 1
      have hg : ‚àë i ‚àà insert j s, g i ‚Ä¢ ùìë i = 0 := by
        unfold g
        rw [Finset.sum_insert hjs, dif_neg hjs, one_smul, ‚Üê Finset.sum_attach]
        simp_rw [dif_pos (Subtype.prop _)]
        exact hkey
      have hb := linearIndependent_iff'.mp ùìë.linearIndependent (insert j s) g hg j
        (Finset.mem_insert_self _ _)
      simp [g, dif_neg hjs] at hb
  refine eq_bot_iff.mpr fun x hx ‚Ü¶ ?_
  obtain ‚ü®s, c, rfl‚ü© := Submodule.mem_span_range_iff_exists'.mp <|
    Submodule.eq_top_iff'.mp (ùìë.baseChange A).span_eq x
  replace main := main s c (by simpa [‚Üê TensorProduct.tmul_eq_smul_one_tmul] using hx)
  simp +contextual [main]

@[simp]
lemma TwoSidedIdeal.span_eq_bot {R : Type*} [NonUnitalNonAssocRing R] {s : Set R} :
    span s = ‚ä• ‚Üî ‚àÄ x ‚àà s, x = 0 := eq_bot_iff.trans
  ‚ü®fun H _ h => (mem_bot R).1 <| H <| subset_span h, fun H =>
    span_le.2 fun x h => (mem_bot R).2 <| H x h‚ü©

lemma TwoSidedIdeal.span_singleton_eq_bot {R : Type*} [NonUnitalNonAssocRing R] {x : R} :
    span ({x} : Set R) = ‚ä• ‚Üî x = 0 := by simp

lemma TwoSidedIdeal.map_bot {R S : Type*}
    [NonUnitalNonAssocRing R] [NonUnitalNonAssocRing S]
    {F : Type*} [FunLike F R S] [ZeroHomClass F R S] {f : F} :
    (‚ä• : TwoSidedIdeal R).map f = ‚ä• := by
  ext x
  simp [map, coe_bot, Set.image_singleton, map_zero f, mem_bot, span_singleton_eq_bot.2]

lemma TensorProduct.map_comap_eq
    {A B : Type v} [DivisionRing A] [Algebra K A] [Ring B] [Algebra K B]
    [isCentral_A : Algebra.IsCentral K A]
    [isSimple_B : IsSimpleRing B]
    (I : TwoSidedIdeal (A ‚äó[K] B)) :
    letI f : B ‚Üí‚Çê[K] A ‚äó[K] B := Algebra.TensorProduct.includeRight
    (I.comap f).map f = I := by
  let f : B ‚Üí‚Çê[K] A ‚äó[K] B := Algebra.TensorProduct.includeRight
  refine (le_antisymm ?_ ?_).symm
  ¬∑ obtain rfl | I_ne_bot := eq_or_ne I ‚ä•
    ¬∑ exact bot_le
    change I ‚â§ TwoSidedIdeal.span (Set.image f <| I.comap f)
    have hI : I.comap f = ‚ä§ := isSimple_B.1.2 _ |>.resolve_left fun r => by
      refine I_ne_bot <| TensorProduct.map_comap_eq_zero_if_zero (hAB := ?_)
      rw [r, TwoSidedIdeal.map_bot]
    rw [hI, TwoSidedIdeal.coe_top, TwoSidedIdeal.le_iff]
    rintro x -
    rw [SetLike.mem_coe]
    induction x using TensorProduct.induction_on with
    | zero => simp
    | tmul a b =>
      rw [show a ‚äó‚Çú[K] b = (a ‚äó‚Çú 1) * (1 ‚äó‚Çú b) by simp]
      exact TwoSidedIdeal.mul_mem_left _ _ _ <| TwoSidedIdeal.subset_span ‚ü®b, ‚ü®‚ü©, rfl‚ü©
    | add x y hx hy => exact TwoSidedIdeal.add_mem _ hx hy
  ¬∑ rw [TwoSidedIdeal.map, TwoSidedIdeal.span_le]
    rintro _ ‚ü®x, hx, rfl‚ü©
    rw [SetLike.mem_coe, TwoSidedIdeal.mem_comap] at hx
    exact hx

lemma TensorProduct.simple' {A B : Type v} [DivisionRing A] [Algebra K A] [Ring B] [Algebra K B]
    [isCentral_A : Algebra.IsCentral K A] [isSimple_B : IsSimpleRing B] :
    IsSimpleRing (A ‚äó[K] B) := by
  let f : B ‚Üí‚Çê[K] A ‚äó[K] B := Algebra.TensorProduct.includeRight
  suffices eq1 : ‚àÄ (I : TwoSidedIdeal (A ‚äó[K] B)),
      I = TwoSidedIdeal.span (Set.image f <| I.comap f) by
    refine ‚ü®‚ü®fun I => ?_‚ü©‚ü©
    specialize eq1 I
    rcases isSimple_B.1.2 (I.comap f) with h|h
    ¬∑ left
      rw [eq1, TwoSidedIdeal.span_eq_bot, h]
      simp
    ¬∑ right
      rw [‚Üê TwoSidedIdeal.one_mem_iff, eq1, h]
      exact TwoSidedIdeal.subset_span ‚ü®1, by simp‚ü©
  exact fun _ ‚Ü¶ TensorProduct.map_comap_eq K _ |>.symm

lemma Matrix.scalar_injective {n Œ± : Type*} [Semiring Œ±] [DecidableEq n]
    [Fintype n] [Nonempty n] : Function.Injective (Matrix.scalar (Œ± := Œ±) n) :=
  fun _ _ h ‚Ü¶ Matrix.scalar_inj.1 h

lemma Matrix.scalarAlgHom_injective (n R Œ± : Type*) [Fintype n] [DecidableEq n] [Nonempty n]
    [CommSemiring R] [Semiring Œ±] [Algebra R Œ±] : Function.Injective (scalarAlgHom n R (Œ± := Œ±)) :=
  Matrix.scalar_injective

lemma Algebra.IsCentral.of_matrix {n D : Type*} [DivisionRing D] [Algebra K D] [Nonempty n]
    [Fintype n] [DecidableEq n] (h : Algebra.IsCentral K (Matrix n n D)) :
    Algebra.IsCentral K D := by
  refine ‚ü®le_of_eq ?_‚ü©
  have := Matrix.subalgebraCenter_eq_scalarAlgHom_map (n := n) (R := K) (A := D)
  rw [center_eq_bot] at this
  apply Subalgebra.map_injective (Matrix.scalarAlgHom_injective n K D)
  simp [‚Üê this]

@[stacks 074C]
instance TensorProduct.simple
    (A B : Type v) [Ring A] [Algebra K A] [Ring B] [Algebra K B]
    [isSimple_A : IsSimpleRing A] [FiniteDimensional K B]
    [isCentral_B : Algebra.IsCentral K B]
    [isSimple_B : IsSimpleRing B] : IsSimpleRing (A ‚äó[K] B) := by
  haveI : IsArtinianRing B := IsArtinianRing.of_finite K B
  obtain ‚ü®n, _, D, _, _, _, ‚ü®e‚ü©‚ü© := IsSimpleRing.exists_algEquiv_matrix_divisionRing_finite K B
  haveI := Algebra.IsCentral.of_matrix K <| Algebra.IsCentral.of_algEquiv K _ _ e
  replace e : A ‚äó[K] B ‚âÉ‚Çê[K] Matrix (Fin n) (Fin n) (D ‚äó[K] A) :=
    (Algebra.TensorProduct.comm K A B).trans <| (Algebra.TensorProduct.congr (e.trans
      (matrixEquivTensor (Fin n) K D)|>.trans <| Algebra.TensorProduct.comm K D _)
      (@AlgEquiv.refl K A ..)).trans <| (Algebra.TensorProduct.assoc K K _ _ _).trans <|
    (Algebra.TensorProduct.comm K _ _).trans <| (matrixEquivTensor (Fin n) _ _).symm
  refine IsSimpleRing.of_ringEquiv e.toRingEquiv.symm <| @IsSimpleRing.matrix _ _ _ _ _ ?_
  exact TensorProduct.simple' K

lemma TensorProduct.map_comap_eq_of_isSimple_isCentralSimple
    {A B : Type v} [Ring A] [Algebra K A] [Ring B] [Algebra K B]
    [isSimple_A : IsSimpleOrder <| TwoSidedIdeal A]
    [isCentral_B : Algebra.IsCentral K B]
    [isSimple_B : IsSimpleRing B]
    (I : TwoSidedIdeal (A ‚äó[K] B)) :
    letI f : A ‚Üí‚Çê[K] A ‚äó[K] B := Algebra.TensorProduct.includeLeft
    (I.comap f).map f = I := by
  classical
  refine (le_antisymm ?_ ?_).symm
  ¬∑ obtain rfl | I_ne_bot := eq_or_ne I ‚ä•
    ¬∑ exact bot_le
    let f : A ‚Üí‚Çê[K] A ‚äó[K] B := Algebra.TensorProduct.includeLeft
    change I ‚â§ TwoSidedIdeal.span (Set.image f <| I.comap f)
    let ùíú := Basis.ofVectorSpace K A
    obtain ‚ü®n, x, ‚ü®x_mem, x_ne_zero, ‚ü®s, card_s, b, rfl‚ü©‚ü©, H‚ü© :=
      is_obtainable_by_sum_tmul.exists_minimal_element _ ùíú I I_ne_bot
    have b_ne_zero : ‚àÄ i ‚àà s, b i ‚â† 0 := by
      by_contra! h
      rcases h with ‚ü®i, h1, h2‚ü©
      specialize H (n - 1) (‚àë i ‚àà s, ùíú i ‚äó‚Çú[K] b i) ‚ü®x_mem, x_ne_zero, ‚ü®s.erase i,
        by rw [Finset.card_erase_of_mem, card_s]; exact h1, b, by
        symm
        fapply Finset.sum_subset
        ¬∑ exact Finset.erase_subset i s
        ¬∑ intro x hx1 hx2
          simp only [Finset.mem_erase, ne_eq, not_and] at hx2
          rw [show x = i by tauto, h2, TensorProduct.tmul_zero]‚ü©‚ü©
      have ineq1 : 0 < n := by
        rw [‚Üê card_s, Finset.card_pos]
        exact ‚ü®i, h1‚ü©
      omega
    obtain rfl | ‚ü®i‚ÇÄ, hi‚ÇÄ‚ü© := s.eq_empty_or_nonempty
    ¬∑ simp at *
    have ineq1 : 0 < n := by
      rw [‚Üê card_s, Finset.card_pos]
      exact ‚ü®i‚ÇÄ, hi‚ÇÄ‚ü©
    have x_eq' :
        ‚àë i ‚àà s, ùíú i ‚äó‚Çú[K] b i =
        ùíú i‚ÇÄ ‚äó‚Çú[K] b i‚ÇÄ +
        ‚àë i ‚àà s.erase i‚ÇÄ, ùíú i ‚äó‚Çú[K] b i := by
      rw [show ùíú i‚ÇÄ ‚äó‚Çú[K] b i‚ÇÄ = ‚àë i ‚àà {i‚ÇÄ}, ùíú i ‚äó‚Çú[K] b i by rw [Finset.sum_singleton],
        ‚Üê Finset.sum_disjUnion]
      pick_goal 2
      ¬∑ simp
      refine Finset.sum_congr ?_ fun _ _ => rfl
      ext x
      simp only [Finset.disjUnion_eq_union, Finset.mem_union, Finset.mem_singleton,
        Finset.mem_erase, ne_eq, or_and_left, em, true_and, iff_or_self]
      simp +contextual [hi‚ÇÄ]
    have span_bi‚ÇÄ : TwoSidedIdeal.span {b i‚ÇÄ} = ‚ä§ := isSimple_B.1.2 _ |>.resolve_left fun r => by
      have mem : b i‚ÇÄ ‚àà (‚ä• : TwoSidedIdeal B) := by
        rw [‚Üê r]
        apply TwoSidedIdeal.subset_span
        simp only [Set.mem_singleton_iff]
      exact b_ne_zero i‚ÇÄ hi‚ÇÄ mem
    have one_mem : (1 : B) ‚àà TwoSidedIdeal.span {b i‚ÇÄ} := by rw [span_bi‚ÇÄ]; trivial
    rw [TwoSidedIdeal.mem_span_iff_exists_fin] at one_mem
    obtain ‚ü®‚Ñê, inst1, xL, xR, y, one_eq‚ü© := one_mem
    replace one_eq : 1 = ‚àë i : ‚Ñê, xL i * b i‚ÇÄ * xR i := by
      rw [one_eq]
      refine Finset.sum_congr rfl fun i _ => ?_
      congr
      simpa only [Set.mem_singleton_iff] using (y i).2
    let œâ := ‚àë i ‚àà s, ùíú i ‚äó‚Çú[K] b i
    let Œ© := ‚àë i : ‚Ñê, (1 ‚äó‚Çú[K] xL i) * œâ * (1 ‚äó‚Çú[K] xR i)
    have Œ©_in_I : Œ© ‚àà I := TwoSidedIdeal.finsetSum_mem _ _ _ fun i _ => I.mul_mem_right _ _ <|
      I.mul_mem_left _ _ x_mem
    have Œ©_eq :
        Œ© =
        ùíú i‚ÇÄ ‚äó‚Çú[K] (‚àë i : ‚Ñê, xL i * b i‚ÇÄ * xR i) +
        ‚àë i ‚àà s.erase i‚ÇÄ, ùíú i ‚äó‚Çú[K] (‚àë j : ‚Ñê, xL j * b i * xR j) := by
      dsimp only [Œ©, œâ]
      simp only [x_eq', mul_add, Algebra.TensorProduct.tmul_mul_tmul, one_mul, Finset.mul_sum,
        add_mul, mul_one, Finset.sum_mul, Finset.sum_add_distrib, TensorProduct.tmul_sum,
        add_right_inj]
      rw [Finset.sum_comm]
    rw [‚Üê one_eq] at Œ©_eq
    have Œ©_prop_1 (b : B) : (1 ‚äó‚Çú b) * Œ© - Œ© * (1 ‚äó‚Çú b) ‚àà I :=
      I.sub_mem (I.mul_mem_left _ _ Œ©_in_I) (I.mul_mem_right _ _ Œ©_in_I)
    have Œ©_prop_2 (x : B) : ((1 : A) ‚äó‚Çú[K] x) * Œ© - Œ© * ((1 : A) ‚äó‚Çú[K] x) =
        ‚àë i ‚àà s.erase i‚ÇÄ, ùíú i ‚äó‚Çú[K]
          (‚àë j : ‚Ñê, (x * (xL j * b i * xR j) - (xL j * b i * xR j) * x)) := by
      rw [Œ©_eq]
      simp [TensorProduct.tmul_sum, mul_add, Algebra.TensorProduct.tmul_mul_tmul, one_mul,
        mul_one, Finset.mul_sum, add_mul, Finset.sum_mul, add_sub_add_left_eq_sub,
        Finset.sum_sub_distrib, TensorProduct.tmul_sub]
    have Œ©_prop_3 (x : B) : ((1 : A) ‚äó‚Çú[K] x) * Œ© - Œ© * ((1 : A) ‚äó‚Çú[K] x) = 0 := by
      by_contra rid
      specialize H (n - 1) (((1 : A) ‚äó‚Çú[K] x) * Œ© - Œ© * ((1 : A) ‚äó‚Çú[K] x))
        ‚ü®Œ©_prop_1 x, rid, ‚ü®s.erase i‚ÇÄ, by rw [Finset.card_erase_of_mem, card_s]; exact hi‚ÇÄ, _,
          Œ©_prop_2 x‚ü©‚ü©
      omega
    simp_rw [Œ©_prop_2] at Œ©_prop_3
    have Œ©_prop_4 : ‚àÄ i ‚àà s.erase i‚ÇÄ,
        ‚àë j : ‚Ñê, (xL j * b i * xR j) ‚àà Subalgebra.center K B := by
      intro i hi
      rw [Subalgebra.mem_center_iff]
      intro x
      specialize Œ©_prop_3 x
      simp only [Finset.mul_sum, Finset.sum_mul, ‚Üê sub_eq_zero, sub_zero]
      rw [‚Üê Finset.sum_sub_distrib, sub_zero]
      have := TensorProduct.sum_tmul_basis_left_eq_zero ùíú (M := A) (N := B) {
        support := (s.erase i‚ÇÄ).filter (fun i ‚Ü¶
          ‚àë j, (x * (xL j * b i * xR j) - xL j * b i * xR j * x) ‚â† 0)
        toFun := fun i ‚Ü¶ if i ‚àà s.erase i‚ÇÄ then (‚àë j : ‚Ñê, (x * (xL j * b i * xR j) -
          xL j * b i * xR j * x)) else 0
        mem_support_toFun := by grind
      } <| by
        simp only [Finsupp.sum, ne_eq, Finset.mem_erase, Finsupp.coe_mk, Finset.sum_filter, ite_not]
        conv_rhs => rw [‚Üê Œ©_prop_3]
        congr! with a ha
        split_ifs with hi hi'
        ¬∑ rw [hi, tmul_zero]
        ¬∑ rfl
        ¬∑ simp only [not_and, Finset.mem_erase, ne_eq] at hi' ha
          exact False.elim <| hi' ha.1 ha.2
      simp only [Finsupp.ext_iff, ne_eq, Finsupp.coe_mk, Finsupp.coe_zero,
        Pi.zero_apply, ite_eq_right_iff] at this
      exact this i hi
    simp_rw [Algebra.IsCentral.center_eq_bot, Algebra.mem_bot, Set.mem_range] at Œ©_prop_4
    choose k hk using Œ©_prop_4
    have Œ©_eq2 := calc Œ©
      _ = ùíú i‚ÇÄ ‚äó‚Çú[K] 1 + ‚àë i ‚àà s.erase i‚ÇÄ, ùíú i ‚äó‚Çú[K] ‚àë j : ‚Ñê, xL j * b i * xR j := Œ©_eq
      _ = ùíú i‚ÇÄ ‚äó‚Çú[K] 1 + ‚àë i ‚àà (s.erase i‚ÇÄ).attach, ùíú i ‚äó‚Çú[K] ‚àë j : ‚Ñê, xL j * b i * xR j := by
          congr 1
          exact Finset.sum_attach _ _ |>.symm
      _ = ùíú i‚ÇÄ ‚äó‚Çú[K] 1 + ‚àë i ‚àà (s.erase i‚ÇÄ).attach, ùíú i ‚äó‚Çú[K] algebraMap _ _ (k i.1 i.2) := by
          congr 1
          refine Finset.sum_congr rfl fun i _ => ?_
          rw [hk i.1 i.2]
      _ = ùíú i‚ÇÄ ‚äó‚Çú[K] 1 +  ‚àë i ‚àà (s.erase i‚ÇÄ).attach, ùíú i ‚äó‚Çú[K] (k i.1 i.2 ‚Ä¢ (1 : B) : B) := by
          congr 1
          refine Finset.sum_congr rfl fun i _ => ?_
          rw [Algebra.algebraMap_eq_smul_one]
      _ = ùíú i‚ÇÄ ‚äó‚Çú[K] 1 + ‚àë i ‚àà (s.erase i‚ÇÄ).attach, (k i.1 i.2 ‚Ä¢ ùíú i) ‚äó‚Çú[K] (1 : B) := by
          congr 1
          refine Finset.sum_congr rfl fun i _ => ?_
          rw [TensorProduct.smul_tmul]
      _ = ùíú i‚ÇÄ ‚äó‚Çú[K] 1 + (‚àë i ‚àà (s.erase i‚ÇÄ).attach, (k i.1 i.2 ‚Ä¢ ùíú i)) ‚äó‚Çú[K] (1 : B) := by
          rw [TensorProduct.sum_tmul]
      _ = (ùíú i‚ÇÄ + (‚àë i ‚àà (s.erase i‚ÇÄ).attach, (k i.1 i.2 ‚Ä¢ ùíú i))) ‚äó‚Çú[K] 1 := by
          rw [TensorProduct.add_tmul]
    rw [Œ©_eq2] at Œ©_in_I
    have hI : I.comap f = ‚ä§ := isSimple_A.2 _ |>.resolve_left fun r => by
      have mem : ùíú i‚ÇÄ + (‚àë i ‚àà (s.erase i‚ÇÄ).attach, (k i.1 i.2 ‚Ä¢ ùíú i)) ‚àà I.comap f := by
        rw [TwoSidedIdeal.mem_comap]
        exact Œ©_in_I
      rw [r] at mem
      change _ = 0 at mem
      rw [mem, TensorProduct.zero_tmul] at Œ©_eq2
      have LI := ùíú.linearIndependent
      rw [linearIndependent_iff'] at LI
      specialize LI s (fun i =>
        if i = i‚ÇÄ then 1
        else if h : i ‚àà s.erase i‚ÇÄ then k i h else 0) (by
        dsimp only
        simp_rw [ite_smul, one_smul, dite_smul, zero_smul]
        rw [Finset.sum_ite, Finset.sum_congr (s‚ÇÅ := s.filter (fun x ‚Ü¶ x = i‚ÇÄ)) (s‚ÇÇ := {i‚ÇÄ})
          (by simp [Finset.ext_iff, hi‚ÇÄ]) (fun _ _ => rfl), Finset.sum_singleton,
          show Finset.filter (fun x ‚Ü¶ ¬¨x = i‚ÇÄ) s = s.erase i‚ÇÄ by grind, ‚Üê Finset.sum_attach]
        conv_rhs => rw [‚Üê mem]
        simp) i‚ÇÄ hi‚ÇÄ
      rw [if_pos rfl] at LI
      exact zero_ne_one LI.symm
    rw [hI, TwoSidedIdeal.coe_top, TwoSidedIdeal.le_iff]
    rintro x -
    rw [SetLike.mem_coe]
    induction x using TensorProduct.induction_on with
    | zero => simp
    | tmul a b =>
      rw [show a ‚äó‚Çú[K] b = (a ‚äó‚Çú 1) * (1 ‚äó‚Çú b) by simp]
      exact TwoSidedIdeal.mul_mem_right _ _ _ <| TwoSidedIdeal.subset_span ‚ü®a, ‚ü®‚ü©, rfl‚ü©
    | add x y hx hy => exact TwoSidedIdeal.add_mem _ hx hy
  ¬∑ rw [TwoSidedIdeal.map, TwoSidedIdeal.span_le]
    rintro _ ‚ü®x, hx, rfl‚ü©
    rw [SetLike.mem_coe, TwoSidedIdeal.mem_comap] at hx
    exact hx

@[stacks 074C]
instance TensorProduct.simple_more_general
    (A B : Type v) [Ring A] [Algebra K A] [Ring B] [Algebra K B]
    [isSimple_A : IsSimpleRing A]
    [isCentral_B : Algebra.IsCentral K B]
    [isSimple_B : IsSimpleRing B] :
    IsSimpleRing (A ‚äó[K] B) := by
  let f : A ‚Üí‚Çê[K] A ‚äó[K] B := Algebra.TensorProduct.includeLeft
  suffices eq1 : ‚àÄ (I : TwoSidedIdeal (A ‚äó[K] B)),
      I = TwoSidedIdeal.span (Set.image f <| I.comap f) by
    refine ‚ü®‚ü®fun I => ?_‚ü©‚ü©
    specialize eq1 I
    rcases isSimple_A.1.2 (I.comap f) with h|h
    ¬∑ left
      rw [eq1, TwoSidedIdeal.span_eq_bot, h]
      simp
    ¬∑ right
      rw [‚Üê TwoSidedIdeal.one_mem_iff, eq1, h]
      exact TwoSidedIdeal.subset_span ‚ü®1, by simp‚ü©
  exact fun _ ‚Ü¶ TensorProduct.map_comap_eq_of_isSimple_isCentralSimple K _ |>.symm
